plugins {
    id 'java'
    id 'jacoco'
    id 'io.quarkus'
    id "org.sonarqube" version "2.8"
    id 'com.diffplug.spotless' version '5.9.0'
}

repositories {
     mavenLocal()
     mavenCentral()
}

dependencies {
    implementation enforcedPlatform("${quarkusPlatformGroupId}:${quarkusPlatformArtifactId}:${quarkusPlatformVersion}")
    implementation 'io.quarkus:quarkus-keycloak-admin-client'
    implementation 'io.quarkus:quarkus-smallrye-openapi'
    implementation 'io.quarkus:quarkus-smallrye-health'
    implementation 'io.quarkus:quarkus-mongodb-panache'
    implementation 'io.quarkus:quarkus-resteasy-jsonb'
    implementation 'io.quarkus:quarkus-oidc'

    testImplementation 'io.quarkus:quarkus-junit5'
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

test.finalizedBy jacocoTestReport

jacoco {
    toolVersion = jacocoVersion
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

spotless {
    format 'misc', {
        target '**/*.gradle', '**/*.properties', '**/*.md', '**/.gitignore', '**/Dockerfile*'
        indentWithSpaces(4)
        trimTrailingWhitespace()
        endWithNewline()
    }
    format 'yaml', {
        target '**/*.yaml'
        indentWithSpaces(2)
        trimTrailingWhitespace()
        endWithNewline()
    }
    java {
        googleJavaFormat()
        indentWithTabs(2)
        indentWithSpaces(4)
    }
    freshmark {
        target 'README.md'
        propertiesFile('gradle.properties')
    }
}
